plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.12'
    id 'io.spring.dependency-management' version '1.1.7'
}

allprojects {
    repositories {
        mavenLocal()
        maven {
            name 'aliyun-public'
            url 'https://maven.aliyun.com/repository/public'
        }
        mavenCentral()
    }
}

subprojects {
    apply {
        plugin('java')
        plugin('org.springframework.boot')
        plugin('io.spring.dependency-management')
    }

    group = 'io.github.oldmerman'
    version = '0.0.1-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    // 通用依赖
    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        implementation 'cn.hutool:hutool-all:5.8.38'
    }

    if (name == 'common-config') {
        dependencies {
            implementation 'org.springframework.boot:spring-boot-starter-web'
            implementation("io.jsonwebtoken:jjwt:0.12.7")
            implementation 'org.springframework.boot:spring-boot-starter-aop'
        }
    }

    if (name == 'merman-model') {
        dependencies {
            implementation 'com.baomidou:mybatis-plus-spring-boot3-starter:3.5.15'
        }
    }

    if (name != 'common-config' && name != 'merman-model') {
        dependencies {
            implementation 'org.springframework.boot:spring-boot-starter-web'
            implementation 'org.springframework.boot:spring-boot-starter-data-redis'
            implementation 'org.springframework.boot:spring-boot-starter-mail'
            implementation 'org.springframework.boot:spring-boot-starter-aop'
            testImplementation 'org.springframework.boot:spring-boot-starter-test'
            testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
            implementation 'com.baomidou:mybatis-plus-spring-boot3-starter:3.5.15'
            runtimeOnly 'com.mysql:mysql-connector-j:9.0.0'
            // mapstruct相关依赖
            implementation 'org.mapstruct:mapstruct:1.5.5.Final'
            annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
            // jwt相关依赖
            implementation 'io.jsonwebtoken:jjwt:0.12.7'
            // 阿里云相关依赖
            implementation ('com.aliyun.oss:aliyun-sdk-oss:3.17.4'){
                exclude group: 'commons-logging', module: 'commons-logging'
            }
            implementation 'javax.xml.bind:jaxb-api:2.3.1'
            implementation 'javax.activation:activation:1.1.1'
            implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.3'

            // 业务模块通常依赖这些基础模块
            implementation project(':common-config')
            implementation project(':merman-model')
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}